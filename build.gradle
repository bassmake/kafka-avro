buildscript {
  repositories {
    gradlePluginPortal()
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.16.0"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:3.26.1"
  }
}

group 'sk.bsmk.hi'
version '1.0-SNAPSHOT'

subprojects {

  apply plugin: 'java'
  apply plugin: 'com.diffplug.gradle.spotless'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://packages.confluent.io/maven/' }
  }

  ext {
    immutablesVersion = '2.8.2'
    confluentVersion = '5.2.1'
    kafkaVersion = '2.3.1'
    jUnitVersion = '5.5.2'
  }

  dependencies {

    compileOnly "org.immutables:value:$immutablesVersion:annotations"
    annotationProcessor "org.immutables:value:$immutablesVersion"


    testRuntime "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testImplementation "com.salesforce.kafka.test:kafka-junit5:3.1.1"
    testImplementation "org.apache.kafka:kafka_2.12:$kafkaVersion"
    testImplementation "org.assertj:assertj-core:3.12.2"
  }

  test {
    useJUnitPlatform()
  }

  spotless {
    java {
      removeUnusedImports()
      googleJavaFormat('1.7')
    }
  }

  compileJava.dependsOn {
    spotlessApply
  }

}

wrapper {
  gradleVersion = '6.0.1'
  distributionType = Wrapper.DistributionType.ALL
}
